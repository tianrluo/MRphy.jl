#= Bloch simulation related
#
# `_pb` as `_pullback`.
=#
using ChainRulesCore

export relax
"""
    mo = relax(mi::StaticVector{3, <:Real}, e1::Real, e2::Real,)

  Apply T1, T2 relaxation on `mi`, with (0., 0., 1.) as assumed equilibrium.

  *Arguments*:
  - `mi`, spin to be relaxed;
  - `e1`, T1 relaxation coeff, exp(-dt/T1);
  - `e2`, T2 relaxation coeff, exp(-dt/T2);
  *Outputs*:
  - `mo`, spin relaxed;
"""
@inline function relax(
  mi::StaticVector{3, <:Real},
  e1::Real,
  e2::Real,
)
  return typeof(mi)(mi[1]*e2, mi[2]*e2, (1-e1)+mi[3]*e1)
end

@inline function irelax(
  mo::StaticVector{3, <:Real},
  e1::Real,
  e2::Real,
)  # TODO: maybe accelerate this by passing in pre-computed reciprocal
  return typeof(mo)(mo[1]/e2, mo[2]/e2, (e1-1+mo[3])/e1)
end


struct CTX_blochsim{T}
  u ::SVector{3, T}  # b/norm(b)
  nb::T              # norm(b)
  cÏ•::T              # cos(Ï•)
  sÏ•::T              # sin(Ï•)
  utmi::T            # uâ‹…mi
end

@inline function ctx_blochsim(
  mi ::SVector{3, <:Real},
  b  ::SVector{3, T},       # Gs
  Î³dt::T=_Î³dt,                   # Rad/Gs â‡ 2Ï€â‹…Hz/Gsâ‹…S
) where {T<:Real}
  # Disassemble the signature-matched `blochsim!` to reuse computations.
  # u, Ï• = b2uÏ•(b, Î³dt) disassembled for reuse
  nb = norm(b)
  u, Ï• = (nb == 0 ? (b, T(0)) : (b/nb, -Î³dt*nb)) # `-Î³dt`: ðµÃ—ð‘€ â†’ ð‘€Ã—ðµ
  cÏ•, sÏ• = cos(Ï•), sin(Ï•)
  utmi = uâ‹…mi

  # mo = uÏ•rot(mi, u, Ï•) disassembled for reuse
  mo = cÏ•*mi + ((1-cÏ•)*utmi)*u + sÏ•*(uÃ—mi)

  ctx = CTX_blochsim(u, nb, cÏ•, sÏ•, utmi)
  return mo, ctx
end

@inline function ctx_blochsim_pb(
  h1,  # Duck type, stability checked by `ChainRulesTestUtils.test_rrule`
  mi ::SVector{3, <:Real},
  mo ::SVector{3, <:Real},
  Î³dt::T,
  ctx::CTX_blochsim{T},
) where {T}
  u, nb, cÏ•, sÏ•, utmi = ctx.u, ctx.nb, ctx.cÏ•, ctx.sÏ•, ctx.utmi
  uth1, h1xu = uâ‹…h1, h1Ã—u

  h0 = cÏ•*h1 + ((1-cÏ•)*uth1)*u + sÏ•*h1xu # uÏ•rot(h1, u, -Ï•)

  ð” Ï•, ð”°Ï• = nb == 0 ? (T(0), -Î³dt) : ((1-cÏ•)/nb, sÏ•/nb)

  nÎ³dtdÏ• = (-Î³dt)*(h1xuâ‹…mo)
  nÎ³dtdu = ð”°Ï•*(miÃ—h1) + (ð” Ï•*utmi)*h1 + (ð” Ï•*uth1)*mi
  db = (nÎ³dtdÏ•-uâ‹…nÎ³dtdu)*u + nÎ³dtdu  # -Î³dtâ‹…(dÏ•â‹…u-1/Ï•â‹…(I-uuáµ€)du)

  return h0, db
end


export blochsim!
"""
    mo = blochsim!(
      mi ::SVector{3, <:Real},
      b  ::AbstractVector{SVector{3, T}};
      mh ::AbstractVector{SVector{3, T}}=similar(b),
      e1 ::Union{Nothing, Real}=nothing,
      e2 ::Union{Nothing, Real}=nothing,
      Î³dt::Real=_Î³dt,
    ) where {T<:Real}

  Bloch simulation.

  *Arguments*:
  - `mi`, input spin states;
  - `b` (nT,) Gauss, rotating frame ðµ-effective sequence;
  *Keyword Arguments*:
  - `mh` (nT,), in-place spin state history under `b`;
  - `e1`, T1 relaxation coeff, exp(-dt/T1);
  - `e2`, T2 relaxation coeff, exp(-dt/T2);
  - `Î³dt` Rad/Gauss, gyro-ratio times simulation temporal step size, dt;
  *Outputs*:
  - `mo`, mh[end], i.e., the output spin state;

  Spin state full history, `mh`, is not explicitly returned, but only accessible
  via the input in-palce argument.
  A future version of this function may return the full history.
"""
function blochsim!(
  mi ::SVector{3, <:Real},
  b  ::AbstractVector{SVector{3, T}};  # Gs
  mh ::AbstractVector{SVector{3, T}}=similar(b),
  e1 ::Union{Nothing, Real}=nothing,
  e2 ::Union{Nothing, Real}=nothing,
  Î³dt::Real=_Î³dt,                      # Rad/Gs â‡ 2Ï€â‹…Hz/Gsâ‹…S
) where {T<:Real}
  @assert length(b) == length(mh)
  @assert (dorelax = e1 !== nothing) == (e2 !== nothing)  # All or none

  Î³dt = eltype(b[end])(Î³dt)  # For type stability
  _relax = dorelax ? @inline(m -> relax(m, e1, e2)) : @inline(m -> m)
  @inbounds for i in eachindex(mh, b)
    mi = (mh[i] = _relax(uÏ•rot(mi, b2uÏ•(b[i], Î³dt)...)))
  end
  return last(mh)
end

"""
    mo = blochsim!(
      mi ::AbstractArray{SVector{3, Tmi}},
      b  ::AbstractArray{Db};  # Gs
      mh ::AbstractArray{Dmh}=similar(b),
      e1 ::Union{Nothing, Real}=nothing,
      e2 ::Union{Nothing, Real}=nothing,
      Î³dt::Real=_Î³dt,
    ) where {
      Tmi<:Real,
      Db<:AbstractVector{SVector{3, T}},
      Dmh<:AbstractVector{SVector{3, T}},
    } where {T<:Real}

  Bloch simulation.

  *Arguments*:
  - `mi` (Nd,), input spin states;
  - `b` {(Nd,) âŠ» (1,)} of (nT,) Gauss, rotating frame ðµ-effective sequences;
  *Keyword Arguments*:
  - `mo` (Nd,), in-place output spin states under `b`;
  - `mh` (Nd,) of (nT,), in-place spin states histories under `b`;
  - `e1`, T1 relaxation coeff, exp(-dt/T1);
  - `e2`, T2 relaxation coeff, exp(-dt/T2);
  - `Î³dt` Rad/Gauss, gyro-ratio times simulation temporal step size, dt;
  *Outputs*:
  - `mo`, (Nd,), output spin states;

  Spin state full history, `mh`, is not explicitly returned, but only accessible
  via the input in-palce argument.
  A future version of this function may return the full history.
"""
function blochsim!(
  mi ::AbstractArray{SVector{3, Tmi}},
  b  ::AbstractArray{Db};  # Gs
  mo ::AbstractArray{SVector{3, Tmi}}=similar(mi),
  mh ::AbstractArray{Dmh}=fill(similar(b[end]), size(mi)),
  e1 ::Union{Nothing, Real}=nothing,
  e2 ::Union{Nothing, Real}=nothing,
  Î³dt::Real=_Î³dt,                      # Rad/Gs â‡ 2Ï€â‹…Hz/Gsâ‹…S
) where {
  Tmi<:Real,
  Db<:AbstractVector{SVector{3, T}},
  Dmh<:AbstractVector{SVector{3, T}},
} where {T<:Real}
  @assert size(mi) == size(b) == size(mh)

  ((mi, b, mh)->blochsim!(mi, b; e1=e1, e2=e2, Î³dt=Î³dt, mh=mh)).(mi, b, mh)

  mo .= last.(mh)
  return mo
end


function ChainRulesCore.rrule(  # could we learn to live right.
  ::typeof(blochsim!),
  mi ::SVector{3, <:Real},
  b  ::AbstractVector{SVector{3, T}};  # Gs
  mh ::AbstractVector{SVector{3, T}}=similar(b),
  e1 ::Union{Nothing, Real}=nothing,
  e2 ::Union{Nothing, Real}=nothing,
  Î³dt::Real=_Î³dt,                      # Rad/Gs â‡ 2Ï€â‹…Hz/Gsâ‹…S
  ctx::AbstractVector{CTX_blochsim{T}}=similar(b, CTX_blochsim{eltype(b[end])}),
  db ::AbstractVector{SVector{3, T}}=similar(b),  # Gs
) where {T<:Real}
  # Dissemble the signature-matched `blochsim!` to reuse computations.
  @assert length(b) == length(mh) == length(ctx) == length(db)
  @assert (dorelax = e1 !== nothing) == (e2 !== nothing)  # All or none

  Î³dt = eltype(b[end])(Î³dt)  # For type stability
  (_relax, _irelax, _E) = (dorelax
    ? (@inline(m -> relax(m, e1, e2)),
       @inline(m -> irelax(m, e1, e2)),
       @inline(v -> v.*(e2, e2, e1)))
    : (@inline(m -> m),
       @inline(m -> m),
       @inline(v -> v))
  )

  inds = eachindex(mh, b, db, ctx)
  _mi = mi
  @inbounds for i in inds
    mh[i], ctx[i] = ctx_blochsim(_mi, b[i], Î³dt)
    _mi = mh[i] = _relax(mh[i])
  end

  """
  Provided `d(mo)`, compute: `d(mi)`, `d(b)`.
  The adpulses paper denotes `{d(mo), d(mi)}` as `{h1, h0}`, respectively.
  """
  function blochsim!_pb(h1) # If we turn back time,
    @inbounds for i in @view(inds[end:-1:begin+1])
      h0 = _E(h1)  # pb of relax
      h1, db[i] = ctx_blochsim_pb(h0, mh[i-1], _irelax(mh[i]), Î³dt, ctx[i])
    end
    h0 = _E(h1)  # pb of relax
    h0, db[begin] = ctx_blochsim_pb(h0, mi, _irelax(mh[begin]), Î³dt, ctx[begin])

    return NoTangent(), h0, db  # How to @thunk and inplace???
  end

  return last(mh), blochsim!_pb
end

