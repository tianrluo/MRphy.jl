#= Bloch simulation related
#
# `_pb` as `_pullback`.
=#
using ChainRulesCore
using StructArrays

export relax
"""
    mo = relax(mi::StaticVector{3, <:Real}, e1::Real, e2::Real,)

  Apply T1, T2 relaxation on `mi`, with (0., 0., 1.) as assumed equilibrium.

  *Arguments*:
  - `mi`, spin to be relaxed;
  - `e1`, T1 relaxation coeff, exp(-dt/T1);
  - `e2`, T2 relaxation coeff, exp(-dt/T2);
  *Outputs*:
  - `mo`, spin relaxed;
"""
@inline function relax(
  mi::StaticVector{3, <:Real},
  e1::Real,
  e2::Real,
)
  return typeof(mi)(mi[1]*e2, mi[2]*e2, (1-e1)+mi[3]*e1)
end

@inline function irelax(
  mo::StaticVector{3, <:Real},
  e1::Real,
  e2::Real,
)  # TODO: maybe accelerate this by passing in pre-computed reciprocal
  return typeof(mo)(mo[1]/e2, mo[2]/e2, (e1-1+mo[3])/e1)
end


struct CTX_blochsim{T}
  u ::SVector{3, T}  # b/norm(b)
  nb::T              # norm(b)
  cœï::T              # cos(œï)
  sœï::T              # sin(œï)
  utmi::T            # u‚ãÖmi
end

@inline function ctx_blochsim(
  mi ::SVector{3, <:Real},
  b  ::SVector{3, T},       # Gs
  Œ≥dt::T=_Œ≥dt,                   # Rad/Gs ‚áê 2œÄ‚ãÖHz/Gs‚ãÖS
) where {T<:Real}
  # Disassemble the signature-matched `blochsim!` to reuse computations.
  # u, œï = b2uœï(b, Œ≥dt) disassembled for reuse
  nb = norm(b)
  u, œï = (nb == 0 ? (b, T(0)) : (b/nb, -Œ≥dt*nb)) # `-Œ≥dt`: ùêµ√óùëÄ ‚Üí ùëÄ√óùêµ
  cœï, sœï = cos(œï), sin(œï)
  utmi = u‚ãÖmi

  # mo = uœïrot(mi, u, œï) disassembled for reuse
  mo = cœï*mi + ((1-cœï)*utmi)*u + sœï*(u√ómi)

  ctx = CTX_blochsim(u, nb, cœï, sœï, utmi)
  return mo, ctx
end

@inline function ctx_blochsim_pb(
  h1,  # Duck type, stability checked by `ChainRulesTestUtils.test_rrule`
  mi ::SVector{3, <:Real},
  mo ::SVector{3, <:Real},
  Œ≥dt::T,
  ctx::CTX_blochsim{T},
) where {T}
  u, nb, cœï, sœï, utmi = ctx.u, ctx.nb, ctx.cœï, ctx.sœï, ctx.utmi
  uth1, h1xu = u‚ãÖh1, h1√óu

  h0 = cœï*h1 + ((1-cœï)*uth1)*u + sœï*h1xu # uœïrot(h1, u, -œï)

  ùî†œï, ùî∞œï = nb == 0 ? (T(0), -Œ≥dt) : ((1-cœï)/nb, sœï/nb)

  nŒ≥dtdœï = (-Œ≥dt)*(h1xu‚ãÖmo)
  nŒ≥dtdu = ùî∞œï*(mi√óh1) + (ùî†œï*utmi)*h1 + (ùî†œï*uth1)*mi
  db = (nŒ≥dtdœï-u‚ãÖnŒ≥dtdu)*u + nŒ≥dtdu  # -Œ≥dt‚ãÖ(dœï‚ãÖu-1/œï‚ãÖ(I-uu·µÄ)du)

  return h0, db
end


export blochsim!
"""
    mo = blochsim!(
      mi ::SVector{3, <:Real},
      b  ::AbstractVector{SVector{3, T}};
      mh ::AbstractVector{SVector{3, T}}=similar(b),
      e1 ::Union{Nothing, Real}=nothing,
      e2 ::Union{Nothing, Real}=nothing,
      Œ≥dt::Real=_Œ≥dt,
    ) where {T<:Real}

  Bloch simulation.

  *Arguments*:
  - `mi`, input spin states;
  - `b` (nT,) Gauss, rotating frame ùêµ-effective sequence;
  *Keyword Arguments*:
  - `mh` (nT,), in-place spin state history under `b`;
  - `e1`, T1 relaxation coeff, exp(-dt/T1);
  - `e2`, T2 relaxation coeff, exp(-dt/T2);
  - `Œ≥dt` Rad/Gauss, gyro-ratio times simulation temporal step size, dt;
  *Outputs*:
  - `mo`, mh[end], i.e., the output spin state;

  Spin state full history, `mh`, is not explicitly returned, but only accessible
  via the input in-palce argument.
  A future version of this function may return the full history.
"""
function blochsim!(
  mi ::SVector{3, <:Real},
  b  ::AbstractVector{SVector{3, T}};  # Gs
  mh ::AbstractVector{SVector{3, T}}=similar(b),
  e1 ::Union{Nothing, Real}=nothing,
  e2 ::Union{Nothing, Real}=nothing,
  Œ≥dt::Real=_Œ≥dt,                      # Rad/Gs ‚áê 2œÄ‚ãÖHz/Gs‚ãÖS
) where {T<:Real}
  @assert length(b) == length(mh)
  @assert (dorelax = e1 !== nothing) == (e2 !== nothing)  # All or none

  Œ≥dt = eltype(b[end])(Œ≥dt)  # For type stability
  _relax = dorelax ? @inline(m -> relax(m, e1, e2)) : @inline(m -> m)
  @inbounds for i in eachindex(mh, b)
    mi = (mh[i] = _relax(uœïrot(mi, b2uœï(b[i], Œ≥dt)...)))
  end
  return last(mh)
end

"""
    mo = blochsim!(
      mi ::AbstractArray{SVector{3, Tmi}},
      b  ::AbstractArray{Db};  # Gs
      mh ::AbstractArray{Dmh}=similar(b),
      e1 ::Union{Nothing, Real}=nothing,
      e2 ::Union{Nothing, Real}=nothing,
      Œ≥dt::Real=_Œ≥dt,
    ) where {
      Tmi<:Real,
      Db<:AbstractVector{SVector{3, T}},
      Dmh<:AbstractVector{SVector{3, T}},
    } where {T<:Real}

  Bloch simulation.

  *Arguments*:
  - `mi` (Nd,), input spin states;
  - `b` {(Nd,) ‚äª (1,)} of (nT,) Gauss, rotating frame ùêµ-effective sequences;
  *Keyword Arguments*:
  - `mo` (Nd,), in-place output spin states under `b`;
  - `mh` (Nd,) of (nT,), in-place spin states histories under `b`;
  - `e1`, T1 relaxation coeff, exp(-dt/T1);
  - `e2`, T2 relaxation coeff, exp(-dt/T2);
  - `Œ≥dt` Rad/Gauss, gyro-ratio times simulation temporal step size, dt;
  *Outputs*:
  - `mo`, (Nd,), output spin states;

  Spin state full history, `mh`, is not explicitly returned, but only accessible
  via the input in-palce argument.
  A future version of this function may return the full history.
"""
function blochsim!(
  mi ::AbstractArray{SVector{3, Tmi}},
  b  ::AbstractArray{Db};  # Gs
  mo ::AbstractArray{SVector{3, Tmi}}=similar(mi),
  mh ::AbstractArray{Dmh}=fill(similar(b[end]), size(mi)),
  e1 ::Union{Nothing, Real}=nothing,
  e2 ::Union{Nothing, Real}=nothing,
  Œ≥dt::Real=_Œ≥dt,                      # Rad/Gs ‚áê 2œÄ‚ãÖHz/Gs‚ãÖS
) where {
  Tmi<:Real,
  Db<:AbstractVector{SVector{3, T}},
  Dmh<:AbstractVector{SVector{3, T}},
} where {T<:Real}
  @assert size(mi) == size(b) == size(mh)

  ((mi, b, mh)->blochsim!(mi, b; e1=e1, e2=e2, Œ≥dt=Œ≥dt, mh=mh)).(mi, b, mh)

  mo .= last.(mh)
  return mo
end


function ChainRulesCore.rrule(  # could we learn to live right.
  ::typeof(blochsim!),
  mi ::SVector{3, <:Real},
  b  ::AbstractVector{SVector{3, T}};  # Gs
  mh ::AbstractVector{SVector{3, T}}=similar(b),
  e1 ::Union{Nothing, Real}=nothing,
  e2 ::Union{Nothing, Real}=nothing,
  Œ≥dt::Real=_Œ≥dt,                      # Rad/Gs ‚áê 2œÄ‚ãÖHz/Gs‚ãÖS
  ctx::AbstractVector{CTX_blochsim{T}}=similar(b, CTX_blochsim{eltype(b[end])}),
  db ::AbstractVector{SVector{3, T}}=similar(b),  # Gs
) where {T<:Real}
  # Dissemble the signature-matched `blochsim!` to reuse computations.
  @assert length(b) == length(mh) == length(ctx) == length(db)
  @assert (dorelax = e1 !== nothing) == (e2 !== nothing)  # All or none

  Œ≥dt = eltype(b[end])(Œ≥dt)  # For type stability
  (_relax, _irelax, _E) = (dorelax
    ? (@inline(m -> relax(m, e1, e2)),
       @inline(m -> irelax(m, e1, e2)),
       @inline(v -> v.*(e2, e2, e1)))
    : (@inline(m -> m),
       @inline(m -> m),
       @inline(v -> v))
  )

  inds = eachindex(mh, b, db, ctx)
  _mi = mi
  @inbounds for i in inds
    mh[i], ctx[i] = ctx_blochsim(_mi, b[i], Œ≥dt)
    _mi = mh[i] = _relax(mh[i])
  end

  """
  Provided `d(mo)`, compute: `d(mi)`, `d(b)`.
  The adpulses paper denotes `{d(mo), d(mi)}` as `{h1, h0}`, respectively.
  """
  function blochsim!_pb(h1) # If we turn back time,
    @inbounds for i in @view(inds[end:-1:begin+1])
      h0 = _E(h1)  # pb of relax
      h1, db[i] = ctx_blochsim_pb(h0, mh[i-1], _irelax(mh[i]), Œ≥dt, ctx[i])
    end
    h0 = _E(h1)  # pb of relax
    h0, db[begin] = ctx_blochsim_pb(h0, mi, _irelax(mh[begin]), Œ≥dt, ctx[begin])

    return NoTangent(), h0, db  # How to @thunk and inplace???
  end

  return last(mh), blochsim!_pb
end

function ChainRulesCore.rrule(  # could we learn to live right.
  ::typeof(blochsim!),
  mi ::AbstractArray{SVector{3, Tmi}},
  b  ::AbstractArray{Db};  # Gs
  mo ::AbstractArray{SVector{3, Tmi}}=similar(mi),
  mh ::AbstractArray{Dmh}=fill(similar(b[end]), size(mi)),
  e1 ::Union{Nothing, Real}=nothing,
  e2 ::Union{Nothing, Real}=nothing,
  Œ≥dt::Real=_Œ≥dt,                      # Rad/Gs ‚áê 2œÄ‚ãÖHz/Gs‚ãÖS
  ctx::AbstractVector{Dctx}
  =fill(similar(b[end], CTX_blochsim{eltype(b[end][end])}), size(mi)),
  h0 ::AbstractArray{SVector{3, Tmi}}=similar(mi),
  db ::AbstractVector{Db}=fill(similar(b[end]), size(mi)),  # Gs
) where {
  Tmi<:Real,
  Db<:AbstractVector{SVector{3, T}},
  Dmh<:AbstractVector{SVector{3, T}},
  Dctx<:AbstractVector{CTX_blochsim{T}},
  Ddb<:AbstractVector{SVector{3, T}},
} where {T<:Real}
  @assert (size(mi) == size(b) == size(mo) == size(mh) ==
    size(ctx) == size(h0) == size(db))

  @inline(_rrule(mi, b, mh, ctx, db) =  # output only scalar pullbacks
    rrule(blochsim!, mi, b; mh=mh, e1=e1, e2=e2, Œ≥dt=Œ≥dt, ctx=ctx, db=db)[2])

  _blochsim!_pb = _rrule.(mi, b, mh, ctx, db)  # scalar pullbacks collection
  mo .= last.(mh)

  function pb_unpack(_pb, h1)
    _, _h0_, _db_ = _pb(h1)
    return (_h0=_h0_, _db=_db_)
  end
  """
  Provided `d(mo)`, compute: `d(mi)`, `d(b)`.
  The adpulses paper denotes `{d(mo), d(mi)}` as `{h1, h0}`, respectively.
  """
  function blochsim!_pb(h1) # If we turn back time,

    sa = StructArray(_h0=h0, _db=db)  # sa for struct array
    sa .= pb_unpack.(_blochsim!_pb, h1)
    return NoTangent(), sa._h0, sa._db
  end

  return mo, blochsim!_pb
end

